cmake_minimum_required(VERSION 2.8.9)

project (mbpoll)

# Appends the cmake/modules path to MAKE_MODULE_PATH variable.
set(CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/cmake" ${CMAKE_MODULE_PATH})

include (GitVersion)
GetGitVersion(MBPOLL_VERSION)
WriteGitVersionFile(${CMAKE_CURRENT_BINARY_DIR}/version-git.h)
set(MBPOLL_VERSION
  ${MBPOLL_VERSION_MAJOR}.${MBPOLL_VERSION_MINOR}.${MBPOLL_VERSION_PATCH})
#message("MBPOLL_VERSION=${MBPOLL_VERSION}")

set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wno-unused-parameter -Wno-unused-function")
#set(CMAKE_BUILD_TYPE Debug)

set(MBPOLL_PKG_VERSION "${MBPOLL_VERSION}")

set(MBPOLL_SRCS src/mbpoll.c
    3rdparty/modbus/modbus.c
    3rdparty/modbus/modbus-data.c
    3rdparty/modbus/modbus-tcp.c
    3rdparty/modbus/modbus-rtu.c
    3rdparty/sysio/delay.c
    3rdparty/sysio/serial.c)

add_executable(mbpoll ${MBPOLL_SRCS})

target_include_directories(mbpoll PUBLIC ${CMAKE_CURRENT_BINARY_DIR})
target_include_directories(mbpoll PUBLIC .)
target_include_directories(mbpoll PUBLIC src)
target_include_directories(mbpoll PUBLIC 3rdparty)
#target_link_libraries(mbpoll ${FTDI_LIBRARIES})
install(TARGETS mbpoll RUNTIME DESTINATION bin)

### Debian Package generation
if (NOT DEFINED PACKAGE_ARCHITECTURE)
    execute_process (
            COMMAND dpkg --print-architecture
            OUTPUT_VARIABLE PACKAGE_ARCHITECTURE
            OUTPUT_STRIP_TRAILING_WHITESPACE
    )
endif()
set(CPACK_GENERATOR "DEB")
set(CPACK_PACKAGE_NAME "${PROJECT_NAME}")
set(CPACK_PACKAGE_VERSION "${MBPOLL_PKG_VERSION}")
set(CPACK_PACKAGE_FILE_NAME "${CPACK_PACKAGE_NAME}_${CPACK_PACKAGE_VERSION}_${PACKAGE_ARCHITECTURE}")
set(CPACK_DEBIAN_PACKAGE_MAINTAINER "Vadim Cherenev <sl@anhot.ru>")
set(CPACK_DEBIAN_PACKAGE_DESCRIPTION "command line utility to communicate with ModBus slave (RTU or TCP)")
set(CPACK_DEBIAN_PACKAGE_ARCHITECTURE "${PACKAGE_ARCHITECTURE}")
include(CPack)
